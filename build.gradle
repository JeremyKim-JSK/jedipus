plugins {
	id 'java'
	id 'pl.allegro.tech.build.axion-release' version '1.2.4'
	id 'maven-publish'
    id "com.jfrog.bintray" version "1.1"   
}

scmVersion {
    tag {
        prefix = 'jedipus'
    }
	
	versionCreator 'versionWithBranch'
    
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v,p -> /jedipus:\d+.\d+.\d+/}, replacement: {v, p -> "jedipus:$v"}]
        pre 'commit'
    }
}

project.group = 'com.fabahaba'
project.version = scmVersion.version

sourceCompatibility = 1.8

repositories {
	maven { url "http://dl.bintray.com/jamespedwards42/libs"  }
    jcenter()
}

dependencies {
	compile 'com.fabahaba:fava:0.7.+'
	compile 'redis.clients:jedis:+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
    
publishing {
	publications {
		mavenJava( MavenPublication ) {
			from components.java
			
			artifact sourcesJar 
        }
    }
}

artifacts {
	archives sourcesJar
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_API_KEY"

    publications = ['mavenJava']
    
    pkg {
        repo = 'libs'
        name = project.name
        desc = 'Extends the executor and marries the command logic of Jedis'
        websiteUrl = 'https://github.com/jamespedwards42/jedipus'
        vcsUrl = 'https://github.com/jamespedwards42/jedipus'
        issueTrackerUrl = 'https://github.com/jamespedwards42/jedipus/issues'
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = project.version
            //desc =  ''
            vcsTag = scmVersion.tag.prefix + '-' + project.version
            gpg {
                sign = true
                passphrase = "$System.env.BINTRAY_GPG_PASSPHRASE"
            }           
        }
    }
}